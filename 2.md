# coffeescript

coffeescript は Javascript を生成する js の上位言語です。

メリット:

* コード量が減る
* 便利なシンタックスシュガーが使える
* 見通しがよくなる

デメリット:

* 最後は Javascript の面倒をみなくてはいけないが、ソースが coffee なのでめんどくさい -> Source map で解決

結論:

じゃあ使うしかないじゃん！？

## インストール

npm を使って簡単にインストールができます。

```
$ npm install -g coffee-script
```

g オプションを付けるとグローバルにインストールされます。早速サンプルを coffee で書き換えていきましょう。

書き換え前(app.js):

```
var express = require("express");
var app = express();
var server = http.createServer(app);
app.use(express.logger());

app.get('/', function(req, res) {
  response.send('Hello World!');
});

var port = process.env.PORT || 3000;
server.listen(port, function() {
  console.log("Listening on " + port);
});
```

書き換え後(app.coffee):

```
express = require "express"
app = express()
server = http.createServer app
app.use express.logger()

app.get '/', (req, res)->
	res.send 'Hello World!'

port = process.env.PORT || 3000
server.listen port, ->
	console.log "Listening on #{port}"
```

短い！ブラケットがない！セミコロンがない！見やすい！！！

ここで使っている文法について、coffeescript では

+ ```var``` を省略できます
+ 関数呼び出しの ```()``` を省略できます(ただし、プロパティとメソッドを区別するため、引数のないメソッドには```()```を付ける必要があります)
+ ```function``` は ```->``` と省略できます
+ 引数のある ```function``` は ```(req, res)->``` のように書きます
+ 関数は ruby や python のようにインデントで表現します
+ 文字列中で ```#{}``` を使うと、文字列中で変数を展開してくれます。

これを ```coffee``` コマンドでコンパイルします。

```
$ coffee -b -c app.coffee
```

coffeescript はデフォルトだと全体を関数で包んでしまうので、それを切るために b オプションをつけています。c オプションでソースファイルを選択します。

## 文法

### this

```@``` は ```this``` に置換されます。

```
@length #=> this.length
```

### 配列、オブジェクト

配列は

```
ary = [
	1
	2
	3
]
```

のように初期化ができます。ちなみに、

```
ary = [1..10]
```

とすると1から10までの数が入った配列が生成できます。```..``` はrange構文と呼ばれています。要素の取り出しも簡単で

```
ary[1..3]
```

とすると1〜3までの要素をスライスした配列を生成できます。

オブジェクトはインデントだけで表現ができます。

```
obj =
	some: "some"
	val: "val"
```

また、代入でもシンタックスシュガーが使えて

```
{some} = obj
console.log some

#=> some
```

という呼び出しができます。また、関数にオブジェクトを引数としてとるときは

```
some.func
	some: "some"
	val: "val"
,
	hoge: "hoge"
	huga: "huga"
```

というようにインデントと ```,``` を組み合わせて関数オブジェクトを引数にとる関数の呼び出しが可能です。

### 演算子

coffee | js
------ | --
is, == | ===
isnt, != | !==
not, ! | !
and, && | &&
or, \|\| | |||
true, yes, on | true
false, no, off | false

これは好みによるところですが、僕は ```||``` などの記号は見難いと思っているので、極力 ```is``` や ```and, or``` のような英文法に従うようなコードを書くようにしています。

### 条件分岐

先の演算子を使って条件分岐を書くことができます。

```
if a is true
	# some
else
	# some
```

また、```else``` 節のない ```if``` は省略して書けます

```
a = b if a is 0
```

js の ```switch``` 節は ```case``` 節ではなく、```when``` と ```else``` を用います。

```
switch a
	when 1
		# some
	when 2 then b = a
	when 3,4,5
		# some
	else
		# some
```
if のように1行で済む処理は ```then``` 節を使ってスッキリと書けます。また、複数の値で同様の処理の場合には ```,``` で区切ることができます。

### 存在演算子

js では、定義されていないプロパティにアクセスしようとするだけで落ちるので、堅牢なコードを書くときにはこまめに存在を確認します。これを js で書くと

```
if(typeof a != "undefined" && a !== null) {
  if(typeof a.some != "undefined" && a.some !== null) {
    a.some()
  }
}
```

これはめんどくさいので、coffee には存在演算子というシンタックスシュガーがあります。

```
if a?.some?
	a.some()
```

と書くだけで同様の js を展開してくれます。

### 繰り返し

繰り返しには for を用います。js では

```
a = ["a", "b", "c"]
for(i in a) {
	console.log(i);
}
#=> 1 2 3
```

となってしまって、なんじゃそりゃ！って感じです。一方 coffee では

```
a = [
"a"
"b"
"c"
]
for i in a
	console.log i
#=> a b c
```

きちんと ```i``` に ```a``` の要素が代入されています。便利。

### this の bind

js では ```this``` の指す先がすごくわかりにくいです。それを解決するのが関数バインドです。構文自体はとても簡単で、関数の

```
->
```

を

```
=>
```

に変えるだけです。たとえば、イベントリスナなんかで ```this``` が変わるときに使えます。

```
f =->
	@some = "some"
	@value = "value"
	elm = $ "p"
	elm.on "click", (e)->
		alert @some, @value

#=> undefined, undefined

f =->
	@some = "some"
	@value = "value"
	elm = $ "p"
	elm.on "click", (e)=>
		alert @some, @value
		
#=> some value

```

### class

みなさんお待ちかねのクラスです。js ではなかなか扱いづらいクラスを coffee では擬似的に扱うことができます。

```
class A
```

メソッドやプロパティの追加は

```
class A
	some: "some"
	val: ->
		console.log "val"
```

のように書きます。インスタンスをつくるときは

```
a = new A()
```

で生成できます。また、生成時に呼ばれるコンストラクタは

```
class A
	constructor:
		@some = "some"
```

と指定できます。また、コンストラクタでは引数に ```@``` を付けるとプロパティにしてくれます

```
class A
	constructor: (@prop, @some)->
	
	show: =>
		console.log @prop, @some

a = new A "prop", "some"
a.show()
#=> prop some
```

クラスの拡張も簡単で

```
class B extends A
```

と書くとAを継承してくれます。

また、private な変数や関数も指定できます。

```
class A
	private = "private"
	
	public: ->
		console.log "private is #{private}"

a = new A()
console.log a.private #=> undefined
console.log a.public  #=> private is private
```

### 例外処理

例外処理も簡潔に書けます。

```
try
	f()
catch err
	console.log err
	throw new Error()
finally
	console.log "finished"
```